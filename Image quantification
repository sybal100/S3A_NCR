//This plugin imports the classified images and quantifies fenestrae area, diameter and area analysed. A results *.txt file will be saved.

run("Clear Results");
waitForUser("INFORMATION","This plugin will use a WEKA probability map and measure the size and diamter of the fenestra and safe a results file");
dir1 = getDirectory("Choose Source Directory");
list = getFileList(dir1);
for (i=0; i<list.length; i++) {
  if (endsWith(list[i], ".tif")) {
    // Open sample
 
    run("Close All");
    open(dir1 + list[i]);
    run("8-bit");
    run("Set Scale...", "distance=144 known=1000 unit=nm");
    
    //quantification of total area
    setSlice(1);
	setAutoThreshold("Default bright");
	run("Set Measurements...", "area perimeter feret's limit display redirect=None decimal=3");
	run("Measure");
	//waitForUser("Action required!","Measurement made?");
	
	//quantification of fenestrae
	setSlice(3);
	setAutoThreshold("Default dark");
	setOption("BlackBackground", true);
	setThreshold(160, 255); //may need to be adopted, lower threshold = less likely a fenestrae, higher threshold = more likely a fenestrae
	run("Set Measurements...", "area perimeter feret's display redirect=None decimal=3");
	run("Analyze Particles...", "size=10-Infinity pixel circularity=0.50-1.00 show=Outlines display exclude include slice");  //size may need to be adopted
	nametif = list[i] + ".tif";
	saveAs("Tif",dir1+nametif);
	nametxt = list[i] + ".txt";
	saveAs("Results",dir1+nametxt);
	//waitForUser("Check if outlines are ok!");
	//close();
	//close();
	run("Close All");
  }
}
